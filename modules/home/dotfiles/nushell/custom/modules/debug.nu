export def debug-watch [
  --exts: string = 'dart,yaml,json'  # è¦ç›‘å¬çš„æ‰©å±•å
  --watch_dirs: list<string> = [
    'lib',
    'assets',
    '../../packages'
  ]  # è¦ç›‘å¬çš„ç›®å½•
  ...args  # ä¼ ç»™ flutter run çš„é¢å¤–å‚æ•°
] {
  # æ£€æŸ¥ä¾èµ–å‘½ä»¤
  let required_cmds = ['fvm', 'tmux', 'xargs']
  for cmd in $required_cmds {
    if (not (which $cmd | is-empty)) == false {
      print $"Error: command not found: ($cmd)"
      return
    }
  }

  # æ£€æŸ¥æ˜¯å¦åœ¨ tmux ä¸­è¿è¡Œ
  if ($env.TMUX? | is-empty) {
    print "âš ï¸  Not inside a tmux session. Please run this inside tmux."
    return
  }

  # ç”Ÿæˆå”¯ä¸€ PID æ–‡ä»¶è·¯å¾„ï¼ˆåŸºäºå½“å‰ç›®å½• hashï¼‰
  let dir_hash = (pwd | hash sha256 | str substring 0..8)
  let pid_path = $"/tmp/-debug-($dir_hash).pid"

  # æ„é€  flutter run å‘½ä»¤
  let flutter_cmd = (
    ['fvm', 'flutter', 'run'] ++ $args ++ [ $"--pid-file=($pid_path)" ]
    | str join ' '
  )

  # å¯åŠ¨ flutter
  tmux send-keys $flutter_cmd Enter

  # å¯åŠ¨ç›‘å¬å™¨åˆ†å±
  tmux split-window -v

  # æ„é€ ç›‘å¬å‘½ä»¤
  let reload_cmd = $'if [ -f "($pid_path)" ]; then cat "($pid_path)" | xargs kill -s USR1; fi'

  let watch_args = ($watch_dirs | each { |dir| $"--watch ($dir)" } | str join ' ')
  let nodemon_cmd = $'npx -y nodemon ($watch_args) -e ($exts) -x "($reload_cmd)"'

  # è¿è¡Œ nodemon
  tmux send-keys $nodemon_cmd Enter

  # è°ƒæ•´ tmux pane
  tmux resize-pane -y 5 -t 1
  tmux select-pane -t 0

  let watchings = ($watch_dirs | str join "\nğŸ“")

  print $"==============\nâœ… Debug-watch Started!\nâœ… Pid file: ($pid_path)\n==============\nâœ… watchingğŸ‘‡ğŸ‘‡ğŸ‘‡\n--------------\nğŸ“($watchings)\n=============="
}
